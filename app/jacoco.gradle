
/**
 * [Android開発のテストカバー率取得にはこのツールを使い分けると良いという話 - Qiita]
 * https://qiita.com/keidroid/items/adc4f065b84d8a2cd17a
 **/

apply plugin: 'com.hiya.jacoco-android'

jacoco {
    toolVersion = "0.8.4" //ツールバージョンを指定可能。省略可。
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    //jacoco.excludes = ['net.vidux.language.Lang']
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    xml.enabled false
    html.enabled true
}

android.applicationVariants.all { variant ->
    def variantName = variant.name.capitalize() //ex. ProdDebug
    def realVariantName = variant.name //ex. prodDebug

    if (variant.buildType.name != "debug") {
        return
    }

    task("jacoco${variantName}TestReport", type: JacocoReport) {
        //AndroidTest後にUnitTestの内容をマージします。
        dependsOn "create${variantName}CoverageReport"
        dependsOn "test${variantName}UnitTest"

        group = "testing"
        description = "Generate Jacoco coverage reports for ${realVariantName}"

        reports {
            xml.enabled = false
            html.enabled = true
        }

        //無視するファイル(excludes)の設定を行います
        def fileFilter = ['**/R.class',
                          '**/R$*.class',
                          '**/BuildConfig.*',
                          '**/Manifest*.*',
                          'android/**/*.*',
                          'androidx/**/*.*',
                          '**/Lambda$*.class',
                          '**/Lambda.class',
                          '**/*Lambda.class',
                          '**/*Lambda*.class',
                          '**/*Lambda*.*',
                          '**/*Builder.*'
        ]

        //Javaファイル対応
        def javaDebugTree = fileTree(dir: "${buildDir}/intermediates/javac/${realVariantName}/compile${variantName}JavaWithJavac/classes", excludes: fileFilter)

        //Kotlinファイル対応
        //def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${realVariantName}", excludes: fileFilter)

        def mainSrc = "${project.projectDir}/src/main/java"
        getSourceDirectories().setFrom(files([mainSrc]))

        //Java,ファイル対応
        getClassDirectories().setFrom(files([javaDebugTree]))

        //Java, Kotlin混在ファイル対応
        //getClassDirectories().setFrom(files([javaDebugTree, kotlinDebugTree]))

        getExecutionData().setFrom(fileTree(dir: project.projectDir, includes: [
                '**/*.exec',    //JUnit Test Result
                //'**/*.ec'     //Espresso Test Result
        ]))
    }

}